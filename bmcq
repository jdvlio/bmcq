#!/usr/bin/awk -f
#
# This script is written to total marks based on a csv file from the Blackboard
# Learn software suite. Its use is LIMITED TO assessments where all the
# multiple choice questions were put in a seperate BB test
#
# **TO USE THIS PROGRAM:**
# All the cases are the same so an example suffices.  Suppose the correct solution
# answering "(c)" for every question and there were 5 questions (it works for
# arbitrarily large sets of multiple choice questions).  If this script is in
# your current working directoy you run the following command:
#
# 		$ ./bmcq -v memo="ccccc"
#
# It serves as proof that we should not turn our backs on older software just
# because we have newer, shinier things that are so convoluted that, at best,
# they barely resemble a working solution.
#
# The old farts that wrote AWK (and other Unix coreutils) are the true heroes
# of this tale and we should learn from their failings as well as their success
# stories!

# define function that totals student marks
function grade(sum) {
	_unused = split($0, entry, ",")	# create an array from the records of the csv file
	for ( i = 0; i < questions; i++ ) {
		if (entry[initial+i*distance] == correct[i+1]) sum += 1
	}
	return sum
}

BEGIN {

	# set configuration variables that might break if BB ever changes their
	# export format but it's simple enough reconfigure.
	initial = 6
	distance = 6
	FS = ","

	# split them memo string to create an array
	questions = split(memo, correct, "")

	for (answer in correct) {
		# properly format the strings in the array
		correct[answer] =  "\"(" correct[answer] ")\""
	}

}

FNR == 1	{
			print $1 "," $2 "," $3 "," "\"Grade[" questions "]\""
		}

FNR > 1	{
			prefix = $1 "," $2 "," $3 ","
			print prefix grade(0)
		}
